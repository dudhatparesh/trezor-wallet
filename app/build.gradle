apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion project.ext.sdk
    defaultConfig {
        applicationId "com.mattskala.trezorwallet"
        minSdkVersion project.ext.minsdk
        targetSdkVersion project.ext.sdk
        versionCode 2
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [
                    crashlyticsEnabled: true
            ]
        }
        debug {
            manifestPlaceholders = [
                    crashlyticsEnabled: false
            ]
            ext.enableCrashlytics = false
        }
    }
    flavorDimensions "coin"
    productFlavors {
        bitcoin {
            dimension "coin"
            buildConfigField("String", "COIN_NAME", "\"Bitcoin\"")
            buildConfigField("int", "COIN_TYPE", "0")
            buildConfigField("String", "BLOCKBOOK_URL", "\"https://btc1.trezor.io\"")
        }
        bitcoinTestnet {
            dimension "coin"
            versionNameSuffix "-testnet"
            buildConfigField("String", "COIN_NAME", "\"Testnet\"")
            buildConfigField("int", "COIN_TYPE", "1")
            buildConfigField("String", "BLOCKBOOK_URL", "\"https://tbtc1.trezor.io\"")
        }
    }
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':trezor-intents')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.0.0"
    implementation("androidx.legacy:legacy-support-v4:1.0.0")
    implementation("androidx.media:media:1.0.0")
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation 'androidx.room:room-runtime:2.1.0'
    kapt 'androidx.room:room-compiler:2.1.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.2'
    implementation 'org.bouncycastle:bcprov-jdk15on:1.62'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.3.0'
    implementation 'org.kodein.di:kodein-di-framework-android-support:5.3.0'
    implementation 'com.github.kenglxn.QRGen:android:2.5.0'
    implementation 'com.dropbox.core:dropbox-core-sdk:3.0.7'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation('io.socket:socket.io-client:1.0.0') {
        // excluding org.json which is provided by Android
        exclude group: 'org.json', module: 'json'
    }
    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'